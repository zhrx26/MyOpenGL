#version 330 core

out vec4 FragColor;

in VS_OUT {
    vec3 FragPos;
    vec3 Normal;
    vec2 TexCoords;
} fs_in;

uniform sampler2D textureID;
uniform vec3 lightPos;
uniform vec3 viewPos;
uniform bool blinn;

void main()
{
    vec3 color = texture(textureID, fs_in.TexCoords).rgb;

    // 环境光
    vec3 ambient = 0.05 * color;
    // 漫反射光
    vec3 lightDir = normalize(lightPos - fs_in.FragPos);
    vec3 normal = normalize(fs_in.Normal);
    float diff = max(dot(lightDir, normal), 0.0);
    vec3 diffuse = diff * color;
    // 镜面光
    vec3 viewDir = normalize(viewPos - fs_in.FragPos);
    float spec = 0.0;
    // Blinn-Phong光照模型
    if(blinn)
    {
        // 将光的方向向量和视线向量相加，得到半程向量
        vec3 halfwayDir = normalize(lightDir + viewDir);  
        spec = pow(max(dot(normal, halfwayDir), 0.0), 16.0);
    }
    // Phong光照模型
    else
    {
        vec3 reflectDir = reflect(-lightDir, normal);
        spec = pow(max(dot(viewDir, reflectDir), 0.0), 8.0);
    }
    vec3 specular = vec3(0.3) * spec; 
    FragColor = vec4(ambient + diffuse + specular, 1.0f);
}
